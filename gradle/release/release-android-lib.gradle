import io.github.naverz.antonio.buildsrc.*

apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.source
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        excludes = ['**/*.kt']
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.source
    }
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    if (JavaVersion.current().isJava9Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addBooleanOption('html5', true)
            }
        }
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }

    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompileProvider.get().classpath.files.join(File.pathSeparator))
        }
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username findProperty("OSSRH_USER_NAME") ?: System.getenv("OSSRH_USER_NAME")
                    password findProperty("OSSRH_PASSWORD") ?: System.getenv("OSSRH_PASSWORD")
                }
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            maven(MavenPublication) {
                from components.release
                groupId = "io.github.naverz"
                artifactId = project.name
                version = Antonio.INSTANCE.getVersionName(project.name)
                artifact androidSourcesJar
                artifact androidJavadocsJar

                pom {
                    signing {
                        def signingKey = findProperty("signingKey") ?: System.getenv("ORG_GRADLE_PROJECT_signingKey")
                        def signingPassword = findProperty("signingPassword") ?: System.getenv("ORG_GRADLE_PROJECT_signingPassword")
                        if (signingKey != null && signingPassword != null) {
                            useInMemoryPgpKeys(signingKey, signingPassword)
                        }
                        sign publishing.publications.maven
                        sign configurations.archives
                    }
                    name = Antonio.NAME
                    description = "Android library for the adapter view (RecyclerView, ViewPager, ViewPager2)"
                    url = "https://github.com/naverz/Antonio/"

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = findProperty("OSSRH_USER_NAME") ?: System.getenv("OSSRH_USER_NAME")
                            name = findProperty("OSSRH_USER_NAME") ?: System.getenv("OSSRH_USER_NAME")
                            email = findProperty("OSSRH_EMAIL") ?: System.getenv("OSSRH_EMAIL")
                        }
                    }
                    scm {
                        connection = "scm:git@github.com:naverz/Antonio.git"
                        developerConnection = "scm:git@github.com:naverz/Antonio.git"
                        url = "https://github.com/naverz/Antonio/"
                    }
                }
            }
        }
    }
}