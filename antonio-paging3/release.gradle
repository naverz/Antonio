import io.github.naverz.antonio.buildsrc.*

apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    publishing {
        repositories {
            maven {

                credentials {
                    username findProperty("OSSRH_USER_NAME") ?: System.getenv("OSSRH_USER_NAME")
                    password findProperty("OSSRH_PASSWORD") ?: System.getenv("OSSRH_PASSWORD")
                }
                def releasesRepoUrl = Maven.REPOSITORY_URL
                def snapshotsRepoUrl = Maven.SNAP_SHOP_REPOSITORY_URL
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            maven(MavenPublication) {
                signing {
                    def signingKey = findProperty("SIGNING_KEY") ?: System.getenv("SIGNING_KEY")
                    def signingPassword = findProperty("SIGNING_PASSWORD") ?: System.getenv("SIGNING_PASSWORD")
                    if (signingKey != null && signingPassword != null) {
                        useInMemoryPgpKeys(signingKey, signingPassword)
                    }
                    sign publishing.publications.maven
                    sign configurations.archives
                }
                from components.release
                groupId = Antonio.GROUP_ID
                artifactId = 'antonio-paging3'
                version = Antonio.VERSION_NAME

                pom {
                    name = Antonio.NAME
                    description = Antonio.DESCRIPTION
                    url = Antonio.REPOSITORY_HTTP_URL

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = findProperty("OSSRH_USER_NAME") ?: System.getenv("OSSRH_USER_NAME")
                            name = findProperty("OSSRH_USER_NAME") ?: System.getenv("OSSRH_USER_NAME")
                            email = findProperty("OSSRH_EMAIL") ?: System.getenv("OSSRH_EMAIL")
                        }
                    }
                    scm {
                        connection = Antonio.REPOSITORY_SCM_GIT_URL
                        developerConnection = Antonio.REPOSITORY_SCM_GIT_URL
                        url = Antonio.REPOSITORY_HTTP_URL
                    }
                }
            }
        }
    }
}